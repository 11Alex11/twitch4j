// Apply the java plugin to add support for Java
plugins {
	id 'idea'
	id 'eclipse'
	id "net.ltgt.apt" version "0.15"
	id 'com.jfrog.bintray' version '1.8.0'
	id "io.spring.dependency-management" version "1.0.5.RELEASE"
}

allprojects {
	apply plugin: "io.spring.dependency-management"
	apply from: "${rootDir}/version.gradle"

	dependencyManagement {
		imports {
			mavenBom "com.fasterxml.jackson:jackson-bom:$jackson_version"
			mavenBom "io.projectreactor:reactor-bom:$reactor_bom"
		}
	}

	repositories {
		jcenter()
		maven { url "https://jitpack.io" }
	}

	// Artifact Info
	group = "com.github.twitch4j"
	description = "This is a api client for the Twitch API V5/IRC/PubSub. (And related services: Streamlabs, ...)"
	version = (System.getenv().containsKey("TRAVIS_TAG")) ? System.getenv("TRAVIS_TAG") : "v0.11.0-SNAPSHOT"

// Doesn't matter, only so that spring doesn't fail
//	mainClassName = 'me.philippheuer.twitch4j.TwitchClient'

	configurations.all {
		// Disable Gradle Caching
		resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
	}
}

subprojects {
	apply plugin: "java"
	apply plugin: "net.ltgt.apt"

	apply from: "${rootDir}/test.gradle"
	apply from: "${rootDir}/deploy.gradle"

	sourceSets {
		main {
			java {
				srcDirs = ['build/generated/source/apt/main', 'src/main/java']
			}
		}
	}

	// Dependencies
	dependencies {

		// Logging
		compile "org.slf4j:slf4j-api:$slf4j_version"

		// Jackson
		compile "com.fasterxml.jackson.core:jackson-databind"
		compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"

		// Project Lombok for annotating processor
		compileOnly "org.projectlombok:lombok:$lombok_version"
		annotationProcessor "org.projectlombok:lombok:$lombok_version"

		// Testing
		testCompile "junit:junit:$junit_version"
		testCompile "io.projectreactor:reactor-test"
		testCompile "org.mockito:mockito-core:$mockito_version"
		testCompile "ch.qos.logback:logback-classic:$logback_version"
	}
}

wrapper {
	gradleVersion = "4.7" // newer version
	distributionType = Wrapper.DistributionType.ALL
}
