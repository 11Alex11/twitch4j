import java.text.SimpleDateFormat

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext {
	projectName = !rootProject.name.equalsIgnoreCase(project.name) ? "${rootProject.name}-${project.name}".toLowerCase() : rootProject.name.toLowerCase()
}

/**
 * Javadoc
 */
javadoc {
	options {
		overview = new File("${rootDir}/src/javadoc/overview.html")
		title = "Twitch 4 Java - ${projectName} (${project.version})"
		windowTitle = "${projectName} (${project.version})"
		stylesheetFile = new File("${rootDir}/src/javadoc/css/style.css")
		encoding = "UTF-8"
	}
}

private def static timestamp() {
	SimpleDateFormat sdf = new SimpleDateFormat("MMM dd yyyy HH:mm:ss zzz")
	sdf.setTimeZone(TimeZone.getTimeZone("GMT"))
	return sdf.format(new Date())
}

jar {
	manifest {
		attributes([
				'Manifest-Version': '1.0',
				'Created-By': "Gradle ${gradle.gradleVersion} - JDK ${System.getProperty("java.specification.version")} (${System.getProperty("java.version")})",
				'Implementation-Title': project.group,
				'Implementation-Vendor': rootProject.name,
		        'Implementation-Version': project.version,
				'Implementation-Date': timestamp()
		])
	}
}

/**
 * Sources - build a jar with source files
 */
task sourcesJar(type: Jar, dependsOn: classes, description: 'Builds the sourcesJar.', group: 'build') {
	from sourceSets.main.allSource
	baseName = projectName.toLowerCase()
	classifier = 'sources'
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc, description: 'Builds the javadocJar.', group: 'build') {
	classifier = 'javadoc'
	baseName = projectName.toLowerCase()
	from javadoc.destinationDir
}

/**
 * Artifacts
 */
artifacts {
	archives sourcesJar
	archives javadocJar
}

/**
 * Deployment
 */
publishing {
	publications {
		mainProject(MavenPublication) {
			from project.components.java
			artifact sourcesJar
			artifact javadocJar
			groupId project.group
			artifactId projectName
			version project.version
		}
	}
}

/**
 * Bintray Upload
 */
bintray {
	user = findProperty("bintrayUser").toString() ?: System.getenv("BINTRAY_USER")
	key = findProperty("bintrayApiKey").toString() ?: System.getenv("BINTRAY_API_KEY")
	publications = ['mainProject']
	dryRun = false 		// Dry Run without deployment
	publish = false 	// automatically publish
	override = false 	// override version artifacts already published
	pkg {
		// jcenter repository namespace and name
		userOrg = 'twitch4j'
		repo = 'maven'
		name = "Twitch4J"
		desc = project.description
		websiteUrl = url
		issueTrackerUrl = "${projectUrl}/issues"
		vcsUrl = gitUrl
		licenses = ['MIT']
		labels = ['twitch', 'kraken', 'helix', 'twitch-pubsub', 'twitch-irc' ]
		publicDownloadNumbers = true
		version {
			name = project.version
			vcsTag = System.getenv("CI_COMMIT_REF_NAME") ?: project.version
			released = Calendar.getInstance(TimeZone.getTimeZone("GMT")).time
		}
	}
}
